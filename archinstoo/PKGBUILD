# shellcheck disable=SC2148,SC2206,SC2034,SC2154
# Release-Online / Non-dev
# Maintainer: Hadean Eon <hadean-eon-dev@proton.me>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Anton Hvornum <torxed@archlinux.org>

pkgname=archinstoo-git
pkgver=r5325.cc1cbcc6
pkgrel=1
pkgdesc="Archinstall revamped"
arch=(any)
url="https://github.com/h8d13/archinstoo"
license=(GPL-3.0-only)
depends=(
  'python-pyparted'
  'python'
  'arch-install-scripts' #For pacstrap, genfstab, chroot
  'coreutils' #Basic utilities
  'util-linux' #For partition utilities
  'pciutils' #For PCI device detection
  'kbd' #For keyboard layout configuration
  'pacman'
  'git'
)
makedepends=(
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
)
optdepends=(
  'systemd: systemd-based operations'
  'btrfs-progs: btrfs filesystem support'
  'dosfstools: FAT EFI filesystem support'
  'e2fsprogs: ext4 filesystem support'
  'f2fs-tools: f2fs filesystem support'
  'ntfs-3g: NTFS filesystem support'
  'xfsprogs: XFS filesystem support'
  'cryptsetup: LUKS encryption support'
  'lvm2: LVM layout support'
  'python-systemd: system journal logging'
)
provides=(archinstoo)
replaces=(archinstoo)
source=("$pkgname::git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname" || exit
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$pkgname/archinstoo" || exit

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname/archinstoo" || exit

  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 docs/archinstoo.1 -t "$pkgdir/usr/share/man/man1/"
}
